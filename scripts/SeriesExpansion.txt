

//* demonstration of polynomial algebraic series expansion


// function defined as standard polynomial
!! f  (x) = f_0 + (f_1*x) + (c_2*x^2) + (c_3*x^3) + (c_4*x^4) + (c_5*x^5) + (c_6*x^6) + (c_7*x^7)
!! f' (x) = f_1 + (2*c_2*x) + (3*c_3*x^2) + (4*c_4*x^3) + (5*c_5*x^4) + (6*c_6*x^5) + (7*c_7*x^6)
!! f''(x) = (2*c_2) + (6*c_3*x) + (12*c_4*x^2) + (20*c_5*x^3) + (30*c_6*x^4) + (42*c_7*x^5)

// Bessel (order n) defined using polynomial descriptions of f f' & f''
!! Bessel (x) = ( x^2 * f''(x) ) + ( x * f'(x) ) + ( (x^2-n^2) * f(x) )

RENDERF f  ;  RENDERF f'  ;  RENDERF f''
RENDERF Bessel
EXPAND Bessel

// Chebyshev equation
!! Chebyshev(z) = (1-z^2) * f''(z) - z * f'(z) + n^2 * f(z)
RENDERF Chebyshev
EXPAND Chebyshev

// save copy of computed tree equations
SAVEJSON Chebyshev
SAVEJSON Bessel

// capture expanded functions
EXPAND Chebyshev T_n
EXPAND Bessel J_n

// Chebyshev initial conditions

//	T	n	  f_0=f(0)	  f_1=f'(0)
//	===============================
//	T0	0		 0			 0
//	T1	1		 0			 1
//	T2	2		-1			 0
//	T3	3		 0			-3
//	T4	4		 1			 0
//	T5	5		 0			 5
//	T6	6		-1			 0
//	T7	7		 0			-7

//	equations for coefficients

//		 n^2 * f_0	+  2 * c_2 = 0
//	 (n^2-1) * f_1	+  6 * c_3 = 0
//	 (n^2-4) * c_2	+ 12 * c_4 = 0
//	 (n^2-9) * c_3	+ 20 * c_5 = 0
//	(n^2-16) * c_4	+ 30 * c_6 = 0
//	(n^2-25) * c_5	+ 42 * c_7 = 0

// define T5

n = 5
f_0 = 0
f_1 = 5

c_2 = - f_0*n^2 / 2
c_3 =  (1-n^2) * f_1 / 6	
c_4 =  (4-n^2) * c_2 / 12
c_5 =  (9-n^2) * c_3 / 20
c_6 = (16-n^2) * c_4 / 30
c_7 = (25-n^2) * c_5 / 42

// show dif between T5 and computed solution

!! cT5 (x) = f_0 + (f_1*x) + (c_2*x^2) + (c_3*x^3) + (c_4*x^4) + (c_5*x^5) + (c_6*x^6) + (c_7*x^7)
GRAPH [-1 <= x < 1.02 <> 0.01]  ( cT5(x) )  

FAMILY Chebyshev 7 First
GRAPH [-1 <= x < 1.02 <> 0.01]  ( T5(x) )  

!!d(x) = T5(x) - cT5(x)
GRAPH [-1 <= x < 1.02 <> 0.01]  ( d(x) )  


// simple polynomials with parameter substitutions
!! S(u) = u + 3  ;  !! T(v) = v + 5
!! R(w) = S(w) * T(w)
EXPAND R

// special case where constant term is 1
!! G(u) = u + 1  ; !! H(v) = v - 1
!! K(s) = G(s) * H(s)
EXPAND K

// sample with sum and product
!! G(u) = u + 3  ; !! H(v) = 1 - 2*v 
!! K(s) = G(s) * H(s)
EXPAND K

// sample with parameters holding sub-expressions
!! G(u) = u - 3  ; !! H(v) = 6 - v 
!! K(s) = G(3*s) * H(2*s)
EXPAND K


// regression test cases from fixed bugs
!! G(u) = u + 1  ; !! H(v) = v - 1
!! K(s) = G(2-3*s) * H(2*s-2)
EXPAND K

// 3   - 3s
// 2s - 3
// 15s -6s2 -9


!! K(s) = G(2-3*s) * H(2*s+2)
EXPAND K

// 3-3s
// 2s+1
// -6s2 3s 3 


!! K(s) = G(2+3*s) * H(2*s+2)
EXPAND K

// 3s+3
// 2s+1
// 6s2 9s 3


